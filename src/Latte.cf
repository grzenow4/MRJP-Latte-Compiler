-- Programs ------------------------------------------------
entrypoints Program ;
Prog.      Program ::= [TopDef] ;
FnDef.     TopDef ::= Type Ident "(" [Arg] ")" Block ;
ClassDef.  TopDef ::= "class" Ident "{" [Field] "}" ;
ClassExt.  TopDef ::= "class" Ident "extends" Ident "{" [Field] "}" ;
AttrDef.   Field ::= Type Ident ";" ;
MethodDef. Field ::= Type Ident "(" [Arg] ")" Block ;
Ar. 	   Arg ::= Type Ident;
separator  Arg "," ;
separator  Field "" ;
separator nonempty TopDef "" ;

-- Statements ----------------------------------------------
Empty.     Stmt ::= ";" ;
Exp.       Stmt ::= Expr ";" ;
Ass.       Stmt ::= Expr "=" Expr ";" ;
Incr.      Stmt ::= Expr "++" ";" ;
Decr.      Stmt ::= Expr "--" ";" ;
Ret.       Stmt ::= "return" Expr ";" ;
VRet.      Stmt ::= "return" ";" ;
If.        Stmt ::= "if" "(" Expr ")" Stmt ;
IfElse.    Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt ;
While.     Stmt ::= "while" "(" Expr ")" Stmt ;
For.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;
BStmt.     Stmt ::= Block ;
Blck.      Block ::= "{" [Stmt] "}" ;
Decl.      Stmt ::= Type [Item] ";" ;
NoInit.    Item ::= Ident ;
Init.      Item ::= Ident "=" Expr ;
separator  Stmt "" ;
separator nonempty Item "," ;

-- Types ---------------------------------------------------
Int.       Type ::= "int" ;
Str.       Type ::= "string" ;
Bool.      Type ::= "boolean" ;
Void.      Type ::= "void" ;
Array.     Type ::= Type "[]" ;
Class.     Type ::= Ident ;

-- Expressions ---------------------------------------------
EOr.       Expr ::= Expr1 "||" Expr ;
EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;
ERel.      Expr2 ::= Expr2 RelOp Expr3 ;
EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;
EMul.      Expr4 ::= Expr4 MulOp Expr5 ;
Not.       Expr5 ::= "!" Expr6 ;
Neg.       Expr5 ::= "-" Expr6 ;
EClass.    Expr6 ::= "new" Ident ;
EAttr.     Expr7 ::= Expr6 "." Ident ;
EMethod.   Expr7 ::= Expr6 "." Ident "(" [Expr] ")" ;
ENull.     Expr7 ::= "(" Ident ")" "null" ;
EArr.      Expr7 ::= "new" Type "[" Expr "]" ;
EElem.     Expr7 ::= Expr7 "[" Expr "]" ;
EVar.      Expr7 ::= Ident ;
EInt.      Expr7 ::= Integer ;
EString.   Expr7 ::= String ;
ETrue.     Expr7 ::= "true" ;
EFalse.    Expr7 ::= "false" ;
EApp.      Expr7 ::= Ident "(" [Expr] ")" ;
coercions  Expr 7 ;
separator  Expr "," ;

-- Operators -----------------------------------------------
Plus.      AddOp ::= "+" ;
Minus.     AddOp ::= "-" ;
Times.     MulOp ::= "*" ;
Div.       MulOp ::= "/" ;
Mod.       MulOp ::= "%" ;
LTH.       RelOp ::= "<" ;
LE.        RelOp ::= "<=" ;
GTH.       RelOp ::= ">" ;
GE.        RelOp ::= ">=" ;
EQU.       RelOp ::= "==" ;
NE.        RelOp ::= "!=" ;

-- Comments ------------------------------------------------
comment    "#" ;
comment    "//" ;
comment    "/*" "*/" ;
